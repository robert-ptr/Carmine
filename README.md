# ðŸ§± Carmine
Is a hardware description language made with the end-goal of generating **working 3D redstone circuits in Minecraft**.

## Language Features
- **Custom language syntax** for defining logic gates, modules, and control flow
- **Compile-time constructs** like loops and if-statements
- **Preprocessor** to evaluate constant expressions
- **Graph-based IR** representing digital logic flow
- **Circuit placer/router** (WIP) for 3D Minecraft redstone layout
- **.dot graph visualization** support for AST and circuit structure

## Roadmap

- [x] Lexer & Parser
- [x] .dot output of AST
- [ ] AST optimization (loop unrolling, constant folding, constant propagation, SSA, dead code elimination) - WIP
- [ ] Module connection logic
- [ ] Placement & routing
- [ ] Redstone layout generation

## Language Grammar
```text
program â†’ { declaration } EOF ;

declaration â†’ moduleStatement
            | varStatement
            | enumStatement
            | statement ;

moduleStatement â†’ "module" IDENTIFIER "(" arguments ")" "->" arguments blockStatement ;

varStatement â†’ "var" IDENTIFIER "(" arguments ")" "->" arguments blockStatement ;

enumStatement â†’ "enum" IDENTIFIER "{" { assignment "," } "}" ";" ;

statement â†’ ifStatement
          | whileStatement
          | forStatement
          | blockStatement
          | expressionStatement ;

blockStatement â†’ "{" { statement } "}" ;

ifStatement â†’ "if" "(" expression ")" blockStatement [ "else" blockStatement ] ;

whileStatement â†’ "while" "(" expression ")" statement ;

forStatement â†’ "for" "(" [ expressionStatement ] [ expression ] ";" [ expression ] ")" statement ;

expressionStatement â†’ expression ";" ;

expression â†’ "var" assignment 
           | "module" assignment
           | assignment
           | or ;

or â†’ and { "|" and } ;

and â†’ equality { "&" equality } ;

equality â†’ comparison { ( "!=" | "==" ) comparison } ;

comparison â†’ term { ( ">" | "<" | ">=" | "<=" ) term } ;

term â†’ factor { ( "+" | "-" ) factor } ;

factor â†’ unary { ( "*" | "/" ) unary } ;

unary â†’ ( "!" | "-" ) unary
       | call ;

call â†’ primary [ "(" arguments ")" ] ;

primary â†’ "true"
        | "false"
        | "null"
        | NUMBER
        | STRING
        | IDENTIFIER
        | IDENTIFIER "[" expression "]"
        | "(" expression ")" ;

arguments â†’ [ expression { "," expression } ] ;

assignment â†’ IDENTIFIER "=" expression ;

NUMBER â†’ DIGIT+ [ "." DIGIT+ ] ;

STRING â†’ "\"" { ALPHA | DIGIT | " " | SYMBOL }* "\"" ;

IDENTIFIER â†’ ALPHA { ALPHA | DIGIT | "_" }* ;

ALPHA â†’ "a" â€¦ "z" | "A" â€¦ "Z" | "_" ;

DIGIT â†’ "0" â€¦ "9" ;

SYMBOL â†’ Any printable symbol excluding quotes and control characters ;
```

## References
Crafting Interpreters - Robert Nystrom  
Digital Design and Computer Architecture (RISC-V Edition) - Sarah L. Harris, David Money Harris  
Engineering a Compiler - Keith D. Cooper, Linda Torczon

## Project Status ðŸš§
This project is currently **unfinished** and is very much a **work in progress**.
